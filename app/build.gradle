plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
    id "com.google.devtools.ksp" version "1.6.21-1.0.5"
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


    defaultConfig {
        applicationId "lab.maxb.dark"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        // Google Client ID
        buildConfigField "String", "GOOGLE_CLIENT_ID", keystoreProperties["GOOGLE_CLIENT_ID"]

        // URL for main API
        buildConfigField "String", "DARK_API_URL", keystoreProperties["DARK_API_URL"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures{
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xcontext-receivers"
    }
    applicationVariants.all { variant ->
        variant.sourceSets.java.each {
            it.srcDirs += "build/generated/ksp/${variant.name}/kotlin"
        }
    }
}

dependencies {
    // Dependency Injection
    def koin_annotations_version = '1.0.0-beta-2'
    implementation 'io.insert-koin:koin-android:3.2.0-beta-1'
    implementation "io.insert-koin:koin-annotations:$koin_annotations_version"
    ksp "io.insert-koin:koin-ksp-compiler:$koin_annotations_version"

    // Coroutines + LiveData
    def coroutines_version = '1.6.1-native-mt'
    def lifecycle_version = '2.4.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Preference
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation "androidx.preference:preference-ktx:1.2.0"

    // Room persistence
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    // Paging
    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Retrofit2 Networking
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.google.code.gson:gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    // Google OAUTH
    implementation 'com.google.android.gms:play-services-auth:20.2.0'

    // Fragment Navigation
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation "androidx.navigation:navigation-runtime-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Android X/KTX
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'

    // Views/Layouts
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // Databinding utils
    implementation 'com.github.wada811:DataBinding-ktx:6.0.0'

    // Desugaring (Time support for API 21+)
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}
