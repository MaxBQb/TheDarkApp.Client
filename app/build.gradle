plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version "1.7.10-1.0.6"
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

ext {
    supportedLocales = ['en', 'ru']
    resToLoc = { res -> res.replaceAll(/-r/, '-').replaceAll(/^b\+/, '').replaceAll(/\+/, '-') }
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    defaultConfig {
        applicationId "lab.maxb.dark"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        // MasterKey for encrypted SharedPreference
        buildConfigField "String", "SECURE_PREFS_MASTER_KEY", keystoreProperties["SECURE_PREFS_MASTER_KEY"]

        // URL for main API
        buildConfigField "String", "DARK_API_URL", keystoreProperties["DARK_API_URL"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs supportedLocales
        buildConfigField "String[]", "LOCALES", '{"' +
                supportedLocales.collect { resToLoc(it) }.join('","') + '"}'
    }

    buildFeatures{
        compose true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.0'
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xcontext-receivers"
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "DarkApp.apk"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    // For KSP
    applicationVariants.configureEach { variant ->
        kotlin.sourceSets {
            getByName(name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    namespace 'lab.maxb.dark'
}

dependencies {
    // Dependency Injection
    def koin_annotations_version = '1.0.1'
    def koin_version = "3.2.0"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"
    implementation "io.insert-koin:koin-annotations:$koin_annotations_version"
    ksp "io.insert-koin:koin-ksp-compiler:$koin_annotations_version"

    // Coroutines + LiveData
    def coroutines_version = '1.6.1-native-mt'
    def lifecycle_version = '2.5.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Serialization
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0'

    // Datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation("io.github.osipxd:encrypted-datastore:1.0.0-alpha02")

    // Room persistence
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    // JC Dialogs
    def dialogs_version = "0.9.0"
    implementation "io.github.vanpra.compose-material-dialogs:core:$dialogs_version"
//    implementation "io.github.vanpra.compose-material-dialogs:color:$dialogs_version"

    // JC Settings
    implementation 'com.github.alorma:compose-settings-ui:0.11.0'

    // Paging
    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation 'androidx.paging:paging-compose:1.0.0-alpha16'

    // Retrofit2 Networking
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.google.code.gson:gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    // JC Navigation
    def destination_version = "1.7.21-beta"
    implementation "io.github.raamcosta.compose-destinations:core:$destination_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$destination_version"

    // Android X/KTX
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.7.0-alpha01'

    // Scalable layout units
    def scalable_units_version = "1.0.6"
    implementation "com.intuit.sdp:sdp-android:$scalable_units_version"
    implementation "com.intuit.ssp:ssp-android:$scalable_units_version"

    // Desugaring (Time-related features support for API 21+)
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'

    // Images with Glide
    def glide_version = "4.13.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:4.11.0"
    implementation ("com.github.bumptech.glide:recyclerview-integration:4.11.0") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    kapt "com.github.bumptech.glide:compiler:$glide_version"
        // Use JC integrated landscapist's Glide version
        def landscapist_glide_version = "2.0.0"
        implementation "com.github.skydoves:landscapist-glide:$landscapist_glide_version"
        implementation "com.github.skydoves:landscapist-animation:$landscapist_glide_version"

    // Compose
    def composeVersion = '1.2.1'
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$composeVersion"
        // Integration with activities
        implementation 'androidx.activity:activity-compose:1.6.0'
        // Theme
        implementation "com.google.android.material:compose-theme-adapter:1.1.20"
        // Compose Material Design
        implementation "androidx.compose.ui:ui-tooling:$composeVersion"
        implementation "androidx.compose.ui:ui-util:$composeVersion"
        implementation 'androidx.compose.material3:material3:1.0.0-rc01'
        // Animations
        implementation "androidx.compose.animation:animation:$composeVersion"
        // Tooling support (Previews, etc.)
        implementation "androidx.compose.ui:ui-tooling:$composeVersion"
        // Integration with ViewModels
        implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
        // ViewPager
        def accompanist_version = "0.26.4-beta"
        implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
        implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
        implementation "com.google.accompanist:accompanist-adaptive:$accompanist_version"

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        // Koin
        testImplementation "io.insert-koin:koin-test:$koin_version"
        testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
        testImplementation "io.insert-koin:koin-test-junit5:$koin_version"
        // JetpackCompose UI
        androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"
}